;;; sndfile.xtm -- working with audio files

;; Author: Andrew Sorensen
;; Keywords: extempore
;; Required dylibs: libsndfile

;;; Commentary:

;; 

;;; Code:

;; lib-loading config
(sys:load-preload-check 'sndfile)

(sys:load "libs/core/audio_dsp.xtm")

;; load the libsndfile dynamic library
(define libsndfile
  (let ((platform (sys:platform)))
    (cond ((string=? platform "Linux") (sys:open-dylib "libsndfile.so"))
          ((string=? platform "Windows") (sys:open-dylib "libsndfile.dll"))
          ((string=? platform "OSX") (sys:open-dylib "libsndfile.dylib"))
          (else (print "Unknown platform: " platform)))))

(if (or (eq? #f libsndfile)
        (null? libsndfile))
    (sys:load-escape "Error loading libsndfile dynamic library"))

(define *xtmlib-sndfile-loaded* #t)

(bind-val SF_LOOP_NONE i32 800)
(bind-val SF_LOOP_FORWARD i32 801)
(bind-val SF_LOOP_BACKWARD i32 802)
(bind-val SF_LOOP_ALTERNATING i32 803)

(bind-val SFC_GET_INSTRUMENT i32 4304) ;; 0x10D0
(bind-val SFC_GET_LOOP_INFO i32 4320) ;; 0x10E0

(bind-type sf_info <i64,i32,i32,i32,i32,i32>)
(bind-type sf_format_info <i32,i8*,i8*>)
;; mode,start,end,count

(bind-type _sf_loop_ <i32,i32,i32,i32>)
;; 270 bytes
;; gain,basenote,detune,velocity_lo,velocity_hi,key_lo,key_hi,loop_count,loops
(bind-type SF_INSTRUMENT <i32,i8,i8,i8,i8,i8,i8,i32,|16,_sf_loop_|>)
;; 44 bytes
;; time_sig_num time_sig_den loop_mode num_beats bpm root_key future[6]
(bind-type SF_LOOP_INFO <|2,i8|,|2,i8|,i32,i32,float,i32,|6,i32|>)
(bind-val SF_FALSE i32 0)
(bind-val SF_TRUE i32 1)

(bind-lib libsndfile sf_open [i8*,i8*,i32,sf_info*]*)
(bind-lib libsndfile sf_close [i32,i8*]*)
(bind-lib libsndfile sf_seek [i64,i8*,i64,i32]*)
(bind-lib libsndfile sf_write_sync [void,i8*]*)
(bind-lib libsndfile sf_read_double [i64,i8*,double*,i64]*)
(bind-lib libsndfile sf_write_double [i64,i8*,double*,i64]*)
(bind-lib libsndfile sf_strerror [i8*,i8*]*)
(bind-lib libsndfile sf_command [i32,i8*,i32,i8*,i32]*)

;; struct accessors

(bind-func sf_frames
  (lambda (info:sf_info*)
    (tref info 0)))

(bind-func sf_samplerate
  (lambda (info:sf_info*)
    (i32toi64 (tref info 1))))

(bind-func sf_channels
  (lambda (info:sf_info*)
    (i32toi64 (tref info 2))))

(bind-func sf_format
  (lambda (info:sf_info*)
    (i32toi64 (tref info 3))))

(bind-func sf_sections
  (lambda (info:sf_info*)
    (i32toi64 (tref info 4))))

(bind-func sf_seekable
  (lambda (info:sf_info*)
    (i32toi64 (tref info 5))))


;; size of audio data in file (in bytes)
(bind-func print_audio_file_info
  (lambda (fname)
    (let ((info:sf_info* (zalloc))
	  (audiofile (sf_open fname 16 info))          
	  (channels (i32toi64 (tref info 2))))
      (printf "audiofile %p\n" audiofile)
      (if (null? audiofile)
          (begin (printf "Bad audio file!\n")
                 void)
          (begin
            (printf "---------------\n")
            (printf "filename:    %s\n" fname)
            (printf "samplerate:  %d\n" (tref info 1))
            (printf "channels:    %d\n" (tref info 2))
            (printf "frames:      %lld\n" (tref info 0))
            (printf "seconds:     %f\n"
                    (/ (i64tod (/ (tref info 0) (i32toi64 (tref info 2))))
                       (i32tod (tref info 1))))
            (let ((loops:SF_LOOP_INFO* (alloc))
                  (lp1 (sf_command audiofile SFC_GET_LOOP_INFO (cast loops i8*) 44)))
              (if (= lp1 SF_TRUE)
                  (printf "loop-mode:   %d - bpm(%f)\n" (tref loops 2) (ftod (tref loops 4)))
                  (printf "loop-mode:\n")))
            (let ((inst:SF_INSTRUMENT* (alloc))
                  (lp2 (sf_command audiofile SFC_GET_INSTRUMENT (cast inst i8*) 270)))
              (if (= lp2 SF_TRUE)
                  (printf "num loops:   %d\n" (tref inst 7))
                  (printf "num loops:   0\n")))
            (sf_close audiofile)
            void)))))


;; an audio buffer reader
;;
;; if dat is set to null then 
;; read_audio_data will return number of frames*channels
(bind-func read_audio_data
  (lambda (fname dat offset num)
    (let ((info:sf_info* (zalloc))
	  (audiofile (sf_open fname 16 info))
	  (channels (i32toi64 (tref info 2)))
	  (num_to_read (* num channels))
	  (num_to_offset (* offset channels))
	  (cnt (sf_seek audiofile num_to_offset 0))
	  (samples_read (sf_read_double audiofile dat num_to_read)))
      (if (<> null audiofile)
          (begin
            (printf "---------------\n")
            (printf "file name:     %s\n" fname)
            (printf "samplerate:    %d\n" (tref info 1))
            (printf "channels:      %d\n" (tref info 2))
            (printf "frames:        %d\n" (tref info 0))
            (printf "samples read:  %lld\n" samples_read))
          (printf "errors:  %s\n" (sf_strerror audiofile)))
      (if (null? dat)
          (* (tref info 0) channels) ;; frames * channels
          samples_read))))


;; an audio buffer reader (returns a value)
(bind-func audiofile_c
  (lambda (fname offset num)
    (let ((info:sf_info* (zalloc))
          (audio_file (sf_open fname 16 info))
	  (channels (sf_channels info))
          (buffer_len (* channels (sf_frames info)))
	  (audio_dat (zalloc buffer_len))
	  (num_to_read (if (= 0 num) buffer_len (* num channels)))
	  (num_to_offset (* offset channels))
	  (cnt (sf_seek audio_file num_to_offset 0))
	  (samples_read (sf_read_double audio_file audio_dat num_to_read))
          (playhead 0))
      (if (<> null audio_file)
          (begin
            (printf "---------------\n")
            (printf "file name:     %s\n" fname)
            (printf "samplerate:    %d\n" (sf_samplerate info))
            (printf "channels:      %d\n" channels)
            (printf "samples read:  %lld\n" samples_read)
            (lambda ()
              (let ((ph playhead))
                channels
                (set! playhead (modulo (+ playhead 1) samples_read))
                (pref audio_dat ph))))
          (begin (printf "errors:  %s\n" (sf_strerror audio_file))
                 (lambda ()
                   (printf "Error: audio_closure wasn't initialised properly.")
                   audio_dat))))))

;; an audio buffer reader (returns a ptr)
(bind-func audiofile_ptr_c
  (lambda (fname offset num)
    (let ((info:sf_info* (zalloc))
          (audio_file (sf_open fname 16 info))
	  (channels (sf_channels info))
          (buffer_len (* channels (sf_frames info)))
	  (audio_dat (zalloc buffer_len))
	  (num_to_read (if (= 0 num) buffer_len (* num channels)))
	  (num_to_offset (* offset channels))
	  (cnt (sf_seek audio_file num_to_offset 0))
	  (samples_read (sf_read_double audio_file audio_dat num_to_read))
          (playhead 0))
      (if (<> null audio_file)
          (begin
            (printf "---------------\n")
            (printf "file name:     %s\n" fname)
            (printf "samplerate:    %d\n" (sf_samplerate info))
            (printf "channels:      %d\n" channels)
            (printf "samples read:  %lld\n" samples_read)
            (lambda ()
              (let ((ph playhead))
                channels
                (set! playhead (modulo (+ playhead 1) samples_read))
                (pref-ptr audio_dat ph))))
          (begin (printf "errors:  %s\n" (sf_strerror audio_file))
                 (lambda ()
                   (printf "Error: audio_closure wasn't initialised properly.")
                   audio_dat))))))

;; write out an audio buffer
(bind-func write_audio_data
  (lambda (fname frames channels:i32 dat)
    (let ((info:sf_info* (zalloc)))
      (tset! info 0 frames)
      (tset! info 1 (dtoi32 SAMPLERATE))
      (tset! info 2 channels)
      (tset! info 3 (+ 131072 2)) ;; + 6 for 32 bit float (+ 2 for 16 bit signed)
      (let ((audiofile (sf_open fname 32 info))
	    (samples_written (sf_write_double audiofile
                                              dat
                                              (* (i32toi64 channels) frames))))
	(if (null? audiofile)
	    (printf "error writing file: %s\n" (sf_strerror audiofile)))
	(sf_write_sync audiofile)
	(sf_close audiofile)
	samples_written))))

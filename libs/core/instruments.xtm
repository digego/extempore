;;; instruments.xtm -- instruments (core)

;; Author: Andrew Sorensen
;; Keywords: extempore

;;; Commentary:

;; This is for instruments which are solely written in scheme and
;; xtlang - no binding to external (that is, C) libraries.
;;
;; Any instruments which depend on external libraries are
;; in libs/external/instruments_ext.xtm

;;; Code:

;; lib-loading config
(sys:load "libs/aot-cache/instruments.xtm" 'quiet)
(sys:load-preload-check 'instruments)

(define *xtmlib-instruments-loaded* #f)

(impc:aot:suppress-aot-do
 (sys:load "libs/core/audio_dsp.xtm"))
(impc:aot:insert-forms (sys:load "libs/core/audio_dsp.xtm" 'quiet))

(impc:aot:insert-header "xtminstruments")

;; making and playing notes


;; Structure for init values of each note
;; start freq amp dur attack decay release sustain
(bind-type NoteInitData <i64,SAMPLE,SAMPLE,i64,i64,i64,i64,SAMPLE>)


;; relative time USING adsr_accum + local mzone
(bind-func note_c
  (lambda (start_time:i64
      freq:SAMPLE
      amp:SAMPLE
      dur:i64
      attack:i64
      decay:i64
      release:i64
      sus_amp:SAMPLE
      nstarts:i64*
      idx:i64
      kernel:[SAMPLE,i64,i64,SAMPLE,SAMPLE]*
      notezone:mzone*)
    (let ((env (if (< (+ attack decay) dur)
                   (adsr_accum_c 0 attack decay (- dur (+ attack decay)) release 1.0 sus_amp)
                   (adsr_accum_c 0 0 0 dur release 1.0 sus_amp)))
          (t 0)
          (ff (lambda (sample:SAMPLE time:i64 channel:i64)
                (if (= channel 0) (set! t (+ t 1)))
                (if (< t (+ dur release))
                    (kernel time channel freq (* (env t channel) amp))
                    (begin (pset! nstarts idx 9999999999999)
                           (llvm_zone_destroy notezone)
                           0.0)))))
      ff)))


;; make synth defaults
(bind-func default_note_c
  (lambda (data:NoteInitData* nargs:i64 dargs:SAMPLE*)
    (let ((sawl (saw_c 0.0))
          (sawr (saw_c 0.0)))
      (lambda (time:i64 chan:i64 freq:SAMPLE amp:SAMPLE)
        (if (= chan 0)
            (* amp (/ 200.0 freq) (sawl amp freq))
            (* amp (/ 200.0 freq) (sawr amp freq)))))))

(bind-func default_fx
  (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
    in))


(bind-func xtm_play_note
  (lambda (time inst:[SAMPLE,SAMPLE,i64,i64,SAMPLE*]* freq amp dur nargs:i64 dargs)
    (if (> freq 10.0)
        (let ((f (inst.new_note:[i64,i64,SAMPLE,i64,SAMPLE,i64,SAMPLE*]*)))
          (f time freq dur amp nargs dargs)
          ;; so that we only copy an integer from memzone
          1)
        1)))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;play for use with native beat shceduler
;;

(bind-func xtm_play
  (lambda (offset:double bsched:[void]* inst:[SAMPLE,SAMPLE,i64,i64,SAMPLE*]*
                    pitch:double vol:double dur:double
                    num:i64 mem:SAMPLE*)
    (let ((bpm (bsched.bpm)))
      (xtm_play_note (+ (now) (dtoi64 (* offset (/ 60.0 bpm) SRd)))
                     inst
                     (midi2frq (convert pitch))
                     (convert (/ (exp (/ vol 26.222)) 127.0))
                     (dtoi64 (* dur (/ 60.0 bpm) SRd))
                     num mem))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Default synth stuff
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(bind-func synth_note_c
  (let ((res 1.0)
        (fxamp 0.002))
    (lambda (data:NoteInitData* nargs:i64 dargs:SAMPLE*)
      (let ((oscl (osc_c 0.0))
            (oscl3 (osc_c 0.0))
            (oscl2 (osc_c 0.0))
            (oscr (osc_c 0.25))
            (saw1 (saw_c 0.0))
            (saw2 (saw_c 0.0))
            (lpf1 (lpf_c))
            (lpf2 (lpf_c))
            (oscr3 (osc_c 0.25))
            (oscr2 (osc_c 0.25)))
        (lambda (time:i64 chan:i64 freq:SAMPLE amp:SAMPLE)
          (cond ((= chan 0)
                 (* amp (/ 5.0 (log freq)) ;; frequency scale amplitude
                    (+ (oscl2 1.0 (+ freq (* 10.0 (random))))
                       (lpf1 (saw1 fxamp freq) (* 5.0 freq) res)
                       (oscl 0.8 (+ freq (oscl3 (* 2.01 freq)
                                                (* freq 2.01)))))))
                ((= chan 1)
                 (* amp (/ 5.0 (log freq)) ;; frequency scale amplitude
                    (+ (oscr2 1.0 (+ freq (* 5.0 (random))))
                       (lpf2 (saw2 fxamp freq) (* 3.0 freq) res)
                       (oscr 0.8 (+ freq (oscr3 (* 0.99 freq)
                                                (* freq 0.99)))))))
                (else 0.0)))))))


(bind-func synth_fx 262144
  (let ((dleft (convert (* SAMPLERATE 0.125)))
        (dlyl (delay_c dleft))
        (dright (convert (* SAMPLERATE 0.33333333)))
        (dlyr (delay_c dright))
        (pan .5)
        (pan_old pan)
        (wet 1.0)
        (fb 0.5))
    (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
      (if (< pan_old pan) ;; interpolate pan
          (set! pan_old (+ pan_old .001))
          (set! pan_old (- pan_old .001)))
      (cond ((= chan 0)
             (dlyl (* 2.0 pan_old in) wet fb))
            ((= chan 1)
             (dlyr (* 2.0 (- 1.0 pan_old) in) wet fb))
            (else 0.0)))))



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; extempore_pad
;;
(bind-func epad_note_c
  (let ((res 0.1))
    (lambda (data:NoteInitData* nargs:i64 dargs:SAMPLE*)
      (let ((oscl (square_c 0.0))
            (oscr (square_c 0.1))
            (o1 (osc_c 0.0))
            (oscl2 (square_c (random)))
            (lpfl (lpf_c))
            (lpfr (lpf_c))
            (oscr2 (square_c (random)))
            (a:SAMPLE (+ 1.0 (* 0.02 (random))))
            (b:SAMPLE (+ 1.0 (* 0.02 (random)))))
        (lambda (time:i64 chan:i64 freq:SAMPLE amp:SAMPLE)
          (if (= chan 0)
              (* amp (lpfl (+ (oscl 0.3 freq)
                              (oscl2 0.3 (+ freq a)))
                           (+ 550.0 (* amp 8000.0))
                           (+ res 0.2)))
              (* amp (lpfr (+ (oscr 0.2 freq)
                              (oscr2 0.2 (* freq b)))
                           (+ 500.0 (* amp 10000.0))
                           res))))))))


(bind-func epad_fx 2000000
  (let ((pan:SAMPLE .5)
        (c1 (chorus_c 0.0))
        (c2 (chorus_c 0.1))
        (rev1 (reverb_c 200.0))
        (rev2 (reverb_c 120.0))
        (ipan pan)
        (cwet 0.5)
        (cfb 0.5)
        (wet 0.2)
        (fb 0.5)
        (opan (osc_c 0.0)))
    (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
      (set! pan (+ 0.5 (opan 0.2 3.0)))
      (cond ((= chan 0)
             (rev1 (c1 (* 2.0 pan in) cwet cfb)
                   wet fb))
            ((= chan 1)
             (rev2 (c2 (* 2.0 (- 1.0 pan) in) cwet cfb)
                   wet fb))
            (else 0.0)))))


;;;;;;;;;;;;;;;;;;;;;
;; extempore organ ;;
;;;;;;;;;;;;;;;;;;;;;

;; change drawbar_pos and recompile to change the organ tone

(bind-func organ_note_c
  (let ((num_drawbars:i64 9)
        (freq_ratio:SAMPLE* (zalloc num_drawbars))
        (drawbar_pos:SAMPLE* (zalloc num_drawbars)))
    (pfill! freq_ratio 0.5 1.5 1.0 2.0 3.0 4.0 5.0 6.0 8.0)
    (pfill! drawbar_pos 8. 8. 8. 0. 0. 0. 0. 0. 0.)
    (lambda (data:NoteInitData* nargs:i64 dargs:SAMPLE*)
      (let ((tonewheel:[SAMPLE,SAMPLE,SAMPLE]** (zalloc (* 2 num_drawbars)))
            (freq_smudge:SAMPLE* (zalloc num_drawbars))
            (i:i64 0))
        (dotimes (i num_drawbars)
          (pset! tonewheel (* i 2) (osc_c 0.0)) ; left
          (pset! tonewheel (+ (* i 2) 1) (osc_c 0.0)) ; right
          (pset! freq_smudge i (* 3.0 (random))))
        (lambda (time:i64 chan:i64 freq:SAMPLE amp:SAMPLE)
          (if (< chan 2)
              (let ((total 0.0))
                (dotimes (i num_drawbars)
                  ;; (printf "i = %lld" i)
                  (set! total (+ total (* (/ (pref drawbar_pos i) 8.0)
                                          ((pref tonewheel (+ (* 2 i) chan))
                                           amp
                                           (+ (* freq (pref freq_ratio i))
                                              (pref freq_smudge i)))))))
                (/ total (convert num_drawbars)))
              0.0))))))

(bind-func organ_fx 100000
  (let ((flanl (flanger_c 1.0 0.0 0.6 1.0))
        (flanr (flanger_c 1.0 0.0 0.6 1.0))
        (treml (osc_c 0.0))
        (tremr (osc_c 0.0))
        (trem_amp 0.1)
        (wet 0.5)
        (fb 0.5)
        (trem_freq .0))
    (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
      (cond ((= chan 0)
             (* (flanl in wet fb)
                (+ 1.0 (treml trem_amp trem_freq))))
            ((= chan 1)
             (* (flanr in wet fb)
                (+ 1.0 (tremr trem_amp (* 1.1 trem_freq)))))
            (else 0.0)))))


;; fuzz bass

(bind-func fuzz_bass_note_c
  (lambda (data:NoteInitData* nargs:i64 dargs:SAMPLE*)
    (let ((sawl1 (saw_c 0.0))
          (sawr1 (saw_c 0.0))
          (sawl2 (saw_c 0.0))
          (sawr2 (saw_c 0.0))
          (smudge1 (+ 1.0 (* 0.001 (dsp_randn))))
          (smudge2 (+ 1.0 (* 0.001 (dsp_randn))))
          (lpfl (lpf_c))
          (lpfr (lpf_c))
          (ringl (ringmod_c))
          (ringr (ringmod_c)))
      (lambda (time:i64 chan:i64 freq:SAMPLE amp:SAMPLE)
        (cond ((= chan 0)
               (lpfl (ringl (+ (sawl1 amp (* freq smudge1))
                               (sawl2 amp (* freq smudge2)))
                            (* 4.0 freq)
                            0.3)
                     (* 8.0 freq)
                     0.5))
              ((= chan 1)
               (lpfr (ringr (+ (sawr1 amp (* freq smudge1))
                               (sawr2 amp (* freq smudge2)))
                            (* 4.0 freq)
                            0.3)
                     (* 8.0 freq)
                     0.5))
              (else 0.0))))))

(bind-func fuzz_bass_fx 10000000
  (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
    in))


;; shimmer pad

(bind-func shimmer_pad_note_c
  (lambda (data:NoteInitData* nargs:i64 dargs:SAMPLE*)
    (let ((rectl (rect_c 0.0))
          (rectr (rect_c 0.0))
          (lfol (osc_c 0.0))
          (lfor (osc_c 0.0))
          (lpfl (lpf_c))
          (lpfr (lpf_c)))
      (lambda (time:i64 chan:i64 freq:SAMPLE amp:SAMPLE)
        (cond ((= chan 0)
               (lpfl (rectl amp freq (+ 0.5 (lfol 0.3 0.9))) (* 8.0 freq) 0.5))
              ((= chan 1)
               (lpfr (rectl amp freq (+ 0.5 (lfor 0.3 1.0))) (* 8.0 freq) 0.5))
              (else 0.0))))))

(bind-func shimmer_pad_fx
  (let ((dl (delay_c 10))
        (dr (delay_c 11))
        (wet 0.5)
        (fb 0.5))
    (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
      (cond ((= chan 0)
             (dl in wet fb))
            ((= chan 1)
             (dr in wet fb))
            (else 0.0)))))



;; A simple FM synth

;; You can push I higher than 1.0
;; But only when H is low
(bind-func fmsynth_note_c
  (lambda (data:NoteInitData* nargs:i64 dargs:SAMPLE*)
    (let ((o1 (osc_c 0.0))
          (o2 (osc_c 0.0))
          (I (if (> nargs 0) (pref dargs 0) 0.1)) ;; modulation index (generally 0.0-1.0)
          (H (if (> nargs 1) (pref dargs 1) 10.0)) ;; harmonicity ratio (whole numbers 1.0 - 20.0)
          (out:SAMPLE 0.0))
      (lambda (time:i64 chan:i64 freq:SAMPLE amp:SAMPLE)
        (if (= chan 0)
            (set! out (o1 (* (/ 5.0 (log freq)) amp)
                          (+ freq
                             (o2 (* (* H freq) I)
                                 (* H freq))))))
        (* 1.0 out)))))


(bind-func fmsynth_fx 700000
  (let ((o1 (osc_c 0.0))
        (o2 (osc_c (* 0.5 SPI)))
        (cl (chorus_c 0.0))
        (cr (chorus_c 0.314))
        (del1 (delay_c (convert (/ (convert SAMPLE_RATE double) 2.0))))
        (del2 (delay_c (convert (/ (convert SAMPLE_RATE double) 3.0))))
        (lpfl (lpf_c))
        (lpfr (lpf_c))
        (res 0.4)
        (revl (reverb_c 70.0))
        (revr (reverb_c 60.0))
        (cwet 0.5)
        (cfb 0.5)
        (dwet 0.5)
        (dfb 0.5)
        (wet 0.2)
        (fb 0.3))
    (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
      (cond ((= chan 0)
             (revl (lpfl (del1 (cl in cwet cfb) dwet dfb)
                         (+ 8000.0 (o1 6000.0 0.3))
                         res)
                   wet
                   fb))
            ((= chan 1)
             (revr (lpfr (del2 (cr in cwet cfb) dwet dfb)
                         (+ 8000.0 (o2 6000.0 0.3))
                         res)
                   wet
                   fb))
            (else 0.0)))))


;; A simple FM pad
;; default is quite organy

;; You can push I higher than 1.0
;; But only when H is low
(bind-func fmpad_note_c
  (lambda (data:NoteInitData* nargs:i64 dargs:SAMPLE*)
    (let ((o1 (osc_c 0.0))
          (o2 (osc_c 0.0))
          (o3 (osc_c 0.0))
          (I (if (> nargs 0) (pref dargs 0) 0.2)) ;; modulation index (generally 0.0-1.0)
          (H (if (> nargs 1) (pref dargs 1) 2.0)) ;; harmonicity ratio (whole numbers 1.0 - 20.0)
          (res:SAMPLE 0.0))
      (lambda (time:i64 chan:i64 freq:SAMPLE amp:SAMPLE)
        (if (= chan 0)
            (set! res (o1 (* (/ 5.0 (log freq)) amp)
                          (+ freq
                             (o2 (* (* H freq) I)
                                 (* H freq))))))
        (* 1.0 res)))))


(bind-func fmpad_fx 700000
  (let ((o1 (osc_c 0.0))
        (o2 (osc_c (* 0.5 SPI)))
        (cl (chorus_c 0.0))
        (cr (chorus_c 0.314))
        (cwet 0.5)
        (cfb 0.5)
        (lpfl (lpf_c))
        (lpfr (lpf_c))
        (res 0.4)
        (revl (reverb_c 70.0))
        (revr (reverb_c 60.0))
        (wet 0.3)
        (fb 0.3))
    (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
      (cond ((= chan 0)
             (revl (lpfl (cl in cwet cfb) ;(del1 (cl in))
                         (+ 8000.0 (o1 6000.0 0.6))
                         res)
                   wet fb))
            ((= chan 1)
             (revr (lpfr (cr in cwet cfb)
                         (+ 8000.0 (o2 6000.0 0.6))
                         res)
                   wet fb))
            (else 0.0)))))


;;
;; simple saw
;;
(bind-func sawbass_note_c
  (lambda (data:NoteInitData* nargs:i64 dargs:SAMPLE*)
    (let ((osc (saw_mc_c 0.0)))
      (lambda (time:i64 chan:i64 freq:SAMPLE amp:SAMPLE)
        (* amp (osc chan amp freq))))))


(bind-func sawbass_fx 500000
  (let ((cl (chorus_mc_c 2 0.0))
        (lpf (lpf_mc_c 2))
        (del (delay_t_st_c
              (convert (* (convert SAMPLE_RATE double) (/ 1.0 2.1)))
              (convert (* (convert SAMPLE_RATE double) (/ 1.0 2.0))) 0))
        (h (hann_t_mc_c 0)))
    (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
      (del chan (* 1.0
                   (lpf chan (cl chan in 1.0 0.6)
                        (+ 350.0 (* 3000.0 (h chan time (convert (* (convert SAMPLE_RATE double) 8.)))))
                        0.75))
           time 0.8 0.5))))

;;
;; simple sine subbass
;;
(bind-func subbass_note_c
  (lambda (data:NoteInitData* nargs:i64 dargs:SAMPLE*)
    (let ((osc (osc_mc_c 0.0)))
      (lambda (time:i64 chan:i64 freq:SAMPLE amp:SAMPLE)
        (* amp (osc chan amp freq))))))

(bind-func subbass_fx
  (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
    in))



(bind-func sawlead_note_c
  (lambda (data:NoteInitData* nargs:i64 dargs:SAMPLE*)
    (let ((sawl1 (saw_c 0.0))
          (sawr1 (saw_c 0.2))
          (sawl2 (saw_c 0.0))
          (sawr2 (saw_c 0.1))
          (smudge1 (+ 1.0 (* 0.005 (dsp_randn))))
          (smudge2 (+ 1.0 (* 0.005 (dsp_randn)))))
      (lambda (time:i64 chan:i64 freq:SAMPLE amp:SAMPLE)
        (cond ((= chan 0)
               (+ (sawl1 amp (* freq smudge1))
                  (sawl2 amp (* freq smudge2))))
              ((= chan 1)
               (+ (sawr1 amp (* freq smudge1))
                  (sawr2 amp (* freq smudge2))))
              (else 0.0))))))

(bind-func sawlead_fx 10000000
  (let ((lfo1l (osc_c 0.0))
        (lfo1r (osc_c 0.3))
        (r1 (reverb_c 200.0))
        (r2 (reverb_c 120.0))
        (rev .15)
        (combl (comb_c 30))
        (combr (comb_c 30)))
    (lambda (in:SAMPLE time:i64 chan:i64 dat:SAMPLE*)
      (cond ((= chan 0)
             (r1 (combl in
                        (+ 4.0 (lfo1l 1.6 2.0))
                        0.8 0.8)
                 rev 0.5))
            ((= chan 1)
             (r2 (combr in
                        (+ 3.0 (lfo1r 1.9 1.0))
                        0.8 0.8)
                 rev 0.5))
            (else 0.0)))))


;; load the scheme macros (e.g. bind-instrument)
(sys:load "libs/core/instruments-scm.xtm")
;; make sure we load them with AOT-compiled code as well
(impc:aot:insert-forms
 (sys:load "libs/core/instruments-scm.xtm"))

(impc:aot:insert-footer "xtminstruments")

(define *xtmlib-instruments-loaded* #t)

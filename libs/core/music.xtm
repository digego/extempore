;;; music.xtm -- Music Composition library

;; Author: Andrew Sorensen
;; Keywords: extempore

;;; Commentary:

;; the start of a music composition library for xtlang
;;

;;; Code:


(if *impc:compiler:with-cache* (sys:load "libs/aot-cache/music.xtm" 'quiet))
(sys:load-preload-check 'music)
(define *xtmlib-music-loaded* #t)

(impc:aot:insert-header "xtmmusic")

;; some 'old-school' sorensen macros

(bind-macro
 ""
 (cosr . args)
 (if (> (length args) 3)
     `(+ ,(cadr args) (* ,(caddr args) (cos (* TWOPI (+ beat ,(car args)) ,(car (cdddr args))))))
     `(+ ,(car args) (* ,(cadr args) (cos (* TWOPI beat ,(caddr args)))))))

(bind-macro
 ""
 (sinr . args)
 (if (> (length args) 3)
     `(+ ,(cadr args) (* ,(caddr args) (sin (* TWOPI (+ beat ,(car args)) ,(car (cdddr args))))))
     `(+ ,(car args) (* ,(cadr args) (sin (* TWOPI beat ,(caddr args)))))))

(bind-macro
 ""
 (tanr . args)
 (if (> (length args) 3)
     `(+ ,(cadr args) (* ,(caddr args) (tan (* TWOPI (+ beat ,(car args)) ,(car (cdddr args))))))
     `(+ ,(car args) (* ,(cadr args) (tan (* TWOPI beat ,(caddr args)))))))

(bind-macro
  "a ramp function, range [-1,1]"
  (ramp x)
  `(let ((xmod2pi:double (- ,x (* TWOPI (floor (/ ,x TWOPI))))))
    (* (/ 1.0 PI) (if (< xmod2pi PI) xmod2pi (- xmod2pi TWOPI)))))

(bind-macro
  (rampr . args)
  (if (> (length args) 3)
      `(+ ,(cadr args) (+ (* .5 ,(caddr args)) (* .5 ,(caddr args) (ramp (* TWOPI (+ beat ,(car args)) ,(cadddr args))))))
      `(+ ,(car args) (+ (* .5 ,(cadr args)) (* .5 ,(cadr args) (ramp (* TWOPI beat ,(caddr args))))))))

(bind-macro
 ""
 (rect x)
 `(let ((xmod2pi:double (- ,x (* TWOPI (floor (/ ,x TWOPI))))))
    (if (< xmod2pi PI) 1.0 0.0)))

(bind-macro
 ""
 (rectr . args)
   (if (> (length args) 3)
       `(+ ,(cadr args) (* ,(caddr args) (rect (* TWOPI (+ beat ,(car args)) ,(car (cdddr args))))))
       `(+ ,(car args) (* ,(cadr args) (rect (* TWOPI beat ,(caddr args)))))))

(bind-macro
  "triangle function, range [0,1]"
  (tri-wave x)
  `(let ((xmod2pi:double (- ,x (* TWOPI (floor (/ ,x TWOPI))))))
    (if (< xmod2pi PI)
        (+ 1.0 (* (/ -1.0 PI) xmod2pi))
        (- (* (/ 1.0 PI) xmod2pi) 1.0))))

(bind-macro
  (trir . args)
  (if (> (length args) 3)
      `(+ ,(cadr args) (* ,(caddr args) (tri-wave (* TWOPI (+ beat ,(car args)) ,(cadddr args)))))
      `(+ ,(car args) (* ,(cadr args) (tri-wave (* TWOPI beat ,(caddr args)))))))

(impc:aot:insert-footer "xtmmusic")
(define *xtmlib-music-loaded* #t)

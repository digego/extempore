;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Extempore's data types library
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; lib-loading config
(sys:load-preload-check 'adt)
(define *xtmlib-adt-loaded* #t)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 
;; fully generalised first and second

(bind-func first:[!a,!b]*
  (lambda (a)
    (tref a 0)))

(bind-func second:[!a,!b]*
  (lambda (a)
    (tref a 1)))

;;;;;;;;;;;;;;;;;;;;
;; points

(bind-type Point <!x,!x>)

(bind-func print:[void,Point*]*
  (lambda (p)
    (if (null? p)
        (begin (printout "<>") void)
        (begin (printout "<"
                         (tref p 0) ","
                         (tref p 1) ">")
               void))))

(bind-func first:[!a,Point:<!a,!a>*]*
  (lambda (a)
    (tref a 0)))

(bind-func second:[!a,Point:<!a,!a>*]*
  (lambda (a)
    (tref a 1)))

;;;;;;;;;;;;;;;;;;;;
;; pairs

(bind-type Pair <!a,!b>)

(bind-func print:[void,Pair*]*
  (lambda (p)
    (if (null? p)
        (begin (printout "<>") void)
        (begin (printout "<" (tref p 0) "," (tref p 1) ">")
               void))))

(bind-func first:[!a,Pair:<!a,!b>*]* 
  (lambda (a)
    (tref a 0)))

(bind-func second:[!b,Pair:<!a,!b>*]*	   
  (lambda (a)
    (tref a 1)))

(bind-func third:[!b,Pair:<!a,!b>*]*
  (lambda (a)
    (tref a 2)))

(bind-func fourth:[!b,Pair:<!a,!b>*]*
  (lambda (a)
    (tref a 3)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; singly linked lists
;;
(bind-type List <!a,List*>)

(bind-func print:[void,List*]*
  (lambda (xs)
    (if (null? xs)
        (begin (printout "()") void)
        (begin
          (printout "(" (car xs))
          (for-each (lambda (x) (printout " " x)) (cdr xs))
          (printout ")")
          void))))

(bind-func tostring:[String*,List*]*
  (lambda (xs)
    (if (null? xs)
        (sprintln "()")
        (let ((ska:String* (sprintout "(" (car xs))))
          (for-each (lambda (xz)
                      (set! ska (sprintout ska " " xz)))
                    (cdr xs))
          (sprintout ska ")")))))


(bind-func cons:[List:<!a,List*>*,!a,List:<!a,List*>*]*
  (lambda (x y)
    (let ((obj (alloc)))
      (tset! obj 0 x)
      (tset! obj 1 y)
      obj)))

(bind-func car:[!a,List:<!a,List*>*]*
  (lambda (a)
    (tref a 0)))

(bind-func cdr:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (a)
    (tref a 1)))

(bind-func set_car:[!a,List:<!a,List*>*,!a]*
  (lambda (a val)
    (tset! a 0 val)))

(bind-func set_cdr:[List:<!a,List*>*,List:<!a,List*>*,,List:<!a,List*>*]*
  (lambda (a val)
    (tset! a 1 val)))

(bind-func length:[i64,List:<!a,List*>*]*
  (lambda (a)    
    (if (null? a)
        0
        (+ 1 (length (cdr a))))))

(bind-func copy:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (lst)
    (let ((orig lst)
          (cpy null))
      (while (not (null? orig))
        (set! cpy (cons (car orig) cpy))
        (set! orig (cdr orig)))
      (reverse cpy))))

(bind-func equal:[List:<!a,List*>*,List:<!a,List*>*,List:<!a,List*>*]*
  "returns true if 'equal' returns true for all pairs of elements"
  (lambda (list1 list2)
    (every (lambda (a b) (equal a b)) list1 list2)))

(bind-func nth_tailr:[List:<!a,List*>*,List:<!a,List*>*,i64]*
  (lambda (lst idx)
    (let ((nt (lambdas (l i cnt)
                       (if (or (= cnt i) (> cnt i) (null? l))
                           l
                           (nt (cdr l)
                               i
                               (+ cnt 1))))))
      (let ((r (nt lst idx 0)))
        r))))

(bind-func nth_tail:[List:<!a,List*>*,List:<!a,List*>*,i64]*
  (lambda (lst idx)
    (if (or (<= idx 0) (null? lst))
        lst
        (nth_tail (cdr lst) (- idx 1)))))

(bind-func nthr:[!a,List:<!a,List*>*,i64]*
  (lambda (lst idx)
    (car (nth_tailr lst idx))))

(bind-func nth:[!a,List:<!a,List*>*,i64]*
  (lambda (lst idx)
    (car (nth_tail lst idx))))

(bind-func insert:[List:<!a,List*>*,List:<!a,List*>*,i64,!a]*
  (lambda (lst idx value)
    (if (= idx 0)
        (cons value lst)
        (let ((pre_cell (nth_tail lst (- idx 1)))
              (new_cell (cons value (cdr pre_cell))))
          (tset! pre_cell 1 new_cell)
          lst))))

(bind-func delete:[!a,List:<!a,List*>*,i64]*
  (lambda (lst idx)
    (let ((pre_cell (nth_tail lst (- idx 1)))
          (cell (cdr pre_cell))
          (post_cell (cdr cell)))
      (tset! pre_cell 1 post_cell)
      (car cell))))

(bind-func reverse_inline:[List:<!a,List*>*,List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (old new)
    (if (null? old) new
        (reverse_inline (cdr old) (cons (car old) new)))))

(bind-func reverse:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (lst)
    (reverse_inline lst (convert null))))

(bind-func reverse_r:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (lst)
    (let ((rev (lambda (old new)
                 (if (null? old)
                     new
                     (rev (cdr old) (cons (car old) new))))))
      (rev lst (convert null)))))

(bind-func map:[List:<!a,List*>*,[!a,!b]*,List:<!b,List*>*]*
  (lambda (func xs)
    (let ((f (lambda (l)
               (if (null? l)
                   null
                   (cons (func (car l)) (f (cdr l)))))))
      (f xs))))

(bind-func map:[List:<!a,List*>*,[!a,!b,!c]*,List:<!b,List*>*,List:<!c,List*>*]*
  (lambda (func xs1 xs2)
    (let ((f (lambda (l1 l2)
               (if (null? l1)
                   null
                   (cons (func (car l1) (car l2))
                         (f (cdr l1) (cdr l2)))))))
      (f xs1 xs2))))

(bind-func map:[List:<!a,List*>*,[!a,!b,!c,!d]*,List:<!b,List*>*,List:<!c,List*>*,List:<!d,List*>*]*
  (lambda (func xs1 xs2 xs3)
    (let ((f (lambda (l1 l2 l3)
               (if (null? l1)
                   null
                   (cons (func (car l1) (car l2) (car l3))
                         (f (cdr l1) (cdr l2) (cdr l3)))))))
      (f xs1 xs2 xs3))))

(bind-func filter:[List:<!a,List*>*,[bool,!a]*,List:<!a,List*>*]*
  (lambda (pred xs)    
    (let ((f (lambda (l)
               (if (null? l)
                   null
                   (if (pred (car l))
                       (cons (car l) (f (cdr l)))
                       (f (cdr l)))))))
      (f xs))))

(bind-func member:[List:<!a,List*>*,!a,List:<!a,List*>*]*
  (lambda (element lst)
    (let ((f (lambda (l)
               (if (null? l)
                   null
                   (if (equal element (car l))
                       l
                       (f (cdr l)))))))
      (f lst))))

(bind-func member:[List:<!a,List*>*,!a,List:<!a,List*>*,[bool,!a,!a]*]*
  (lambda (element lst pred)
    (let ((f (lambda (l)
               (if (null? l)
                   null
                   (if (pred element (car l))
                       l
                       (f (cdr l)))))))
      (f lst))))

(bind-func any:[bool,[bool,!a]*,List:<!a,List*>*]*
  (lambda (pred lst)
    (let ((f (lambda (l)
               (if (null? l)
                   #f
                   (if (pred (car l))
                       #t
                       (f (cdr l)))))))
      (f lst))))

(bind-func any:[bool,[bool,!a,!b]*,List:<!a,List*>*,List:<!b,List*>*]*
  (lambda (pred lst1 lst2)
    (let ((f (lambda (l1 l2)
               (if (or (null? l1) (null? l2))
                   #f
                   (if (pred (car l1) (car l2))
                       #t
                       (f (cdr l1) (cdr l2)))))))
      (f lst1 lst2))))

(bind-func every:[bool,[bool,!a]*,List:<!a,List*>*]*
  (lambda (pred lst)
    (let ((f (lambda (l)
               (if (null? l)
                   #t
                   (if (not (pred (car l)))
                       #f
                       (f (cdr l)))))))
      (f lst))))

(bind-func every:[bool,[bool,!a,!b]*,List:<!a,List*>*,List:<!b,List*>*]*
  (lambda (pred lst1 lst2)
    (let ((f (lambda (l1 l2)
               (if (or (null? l1) (null? l2))
                   #t
                   (if (not (pred (car l1) (car l2)))
                       #f
                       (f (cdr l1) (cdr l2)))))))
      (f lst1 lst2))))

(bind-func for-each:[void,[!a,!b]*,List:<!b,List*>*]*
  (lambda (func xs)
    (let ((f (lambda (l)
               (if (null? l) void
                   (begin (func (car l))
                          (f (cdr l)))))))
      (f xs))))

(bind-func for-each:[void,[!a,!b,!c]*,List:<!b,List*>*,List:<!c,List*>*]*
  (lambda (func xs1 xs2)
    (let ((f (lambda (l1 l2)
               (if (null? l1) void
                   (begin (func (car l1) (car l2))
                          (f (cdr l1) (cdr l2)))))))
      (f xs1 xs2))))

(bind-func for-each:[void,[!a,!b,!c,!d]*,List:<!b,List*>*,List:<!c,List*>*,List:<!d,List*>*]*
  (lambda (func xs1 xs2 xs3)
    (let ((f (lambda (l1 l2 l3)
               (if (null? l1) void
                   (begin (func (car l1) (car l2) (car l3))
                          (f (cdr l1) (cdr l2) (cdr l3)))))))
      (f xs1 xs2 xs3))))

(bind-func last:[List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (a)
    (if (null? (cdr a))
        a
        (last (cdr a)))))

(bind-func range:[List:<!a,List*>*,!a]*
  (lambda (end)
    (let ((val (- end (convert 1)))
          (result null))
      (while (>= val (convert 0))
        (set! result (cons val result))
        (set! val (- val (convert 1))))
      result)))

(bind-func range:[List:<!a,List*>*,!a,!a]*
  (lambda (start end)
    (let ((val (- end (convert 1)))
          (result null))
      (while (>= val start)
        (set! result (cons val result))
        (set! val (- val (convert 1))))
      result)))

(bind-func range:[List:<!a,List*>*,!a,!a,!a]*
  (lambda (start end step)
    (if (<= step 0)
        (begin (println "Problem in call to range: step must be positive")
               null)
        (let ((val start)
              (result null))
          (while (< val end)
            (set! result (cons val result))
            (set! val (+ val step)))
          (reverse result)))))

(bind-func append:[List:<!a,List*>*,List:<!a,List*>*,List:<!a,List*>*]*
  (lambda (a b)
    (if (null? a) b
        (begin
          (tset! (last a) 1 b)
          a))))

(bind-func swap:[List:<!a,List*>*,List:<!a,List*>*,i64,i64]*
  ;; swap ith and jth list elements inplace  
  (lambda (lst i j)
    (let ((ith_val (nth lst i)))
      (set_car (nth_tail lst i) (nth lst j))
      (set_car (nth_tail lst j) ith_val))
    lst))

;; fold/reduce

(bind-func foldl:[!b,[!b,!b,!a]*,!b,List:<!a,List*>*]*
  (lambda (fn start lst)
    (let ((fold (lambda (l r) ; list result
                  (if (null? l)
                      r
                      (fold (cdr l) (fn r (car l)))))))
      (fold lst start))))

(bind-func foldr:[!b,[!b,!a,!b]*,!b,List:<!a,List*>*]*
  (lambda (fn end lst)
    (let ((fold (lambda (l r) ; list result
                  (if (null? l)
                      r
                      (fold (cdr l) (fn (car l) r))))))
      (fold (reverse lst) end))))

(bind-func take:[List:<!a,List*>*,List:<!a,List*>*,i64]*
  "returns a copy of the first n elements of lst"
  (lambda (lst n)
    (let ((newlst (list))
          (oldlst lst)
          (count 0))
      (while (and (< count n)
                  (not (null? oldlst)))
        (set! newlst (cons (car oldlst) newlst))
        (set! oldlst (cdr oldlst))
        (set! count  (+ count 1)))
      (reverse newlst))))

(bind-func drop:[List:<!a,List*>*,List:<!a,List*>*,i64]*
  "returns a copy of lst sans the first n elements"
  (lambda (lst n)
    (copy (nth_tail lst n))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; a simple 'assoc' dictionary
;;
;; the "dictionary" is any List of Pairs
;; where the pair is any <i8*,!a>
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(bind-func dict_find:[!z,List:<Pair:<String*,!z>*,List*>*,String*]*
  (lambda (dict key)
    (if (null? dict) (convert null)
        (if (string_cmp (first (car dict)) key)
            (second (car dict))
            (dict_find (cdr dict) key)))))


;;;;;;;;;;;
;; TREES ;;
;;;;;;;;;;;

;; start of binary tree ...

(bind-type BTree <!_a,BTree*,BTree*>)

(bind-func print:[void,BTree*]*
  (lambda (t)
    (printout "<")    
    (let ((f (lambda (t)
               (if (null? t) void
                   (begin
                     (printout (btree_value t) " ")
                     (f (tref t 1))
                     (f (tref t 2))
                     void)))))      
      (f t)
      (printout ">")
      void)))


(bind-func BLeaf:[BTree:<!a,BTree*,BTree*>*,!a]*
  (lambda (x)
    (let ((t (BTree x null null)))
      t)))

(bind-func btree_value:[!a,BTree*]*
  (lambda (t)
    (tref t 0)))


;; functor for Tree
(bind-func fmap:[BTree:<!a,BTree*,BTree*>*,[!a,!b]*,BTree:<!b,BTree*,BTree*>*]*  
  (lambda (func t)
    (if (null? t) null
        (BTree (func (tref t 0))
               (fmap func (tref t 1))
               (fmap func (tref t 2))))))

;; (bind-func test_tree_as_functor
;;   (lambda ()
;;     (let ((t (BTree 4 (BTree 2 (BLeaf 1) (BLeaf 3)) (BLeaf 5)))
;;           (t1 (fmap (lambda (x:i64) x) t)) ;; identity should return identity
;;           (f1 (lambda (x) (i64tod x)))
;;           (f2 (lambda (x:double) (* x x)))
;;           (t2 (fmap f2 (fmap f1 t)))
;;           (t3 (fmap (lambda (x) (f2 (f1 x))) t)))
;;       (println "functor passes identity test" t "==" t1)
;;       (println "functor passes composition test" t2 "==" t3)
;;       void)))



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Some extra string stuff
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(bind-func regex_match_a
  (lambda (regex str)
    (rmatch regex (string_cstr str))))

(bind-func regex_match_b
  (lambda (regex str)
    (rmatch (string_cstr regex) (string_cstr str))))

(bind-poly regex_match regex_match_a)
(bind-poly regex_match regex_match_b)

(bind-func regex_split_a
  (lambda (regex str)
    (let ((a:i8* (salloc 2048))
          (b:i8* (salloc 2048)))
      (sprintf a "")
      (sprintf b "")
      (rsplit regex (string_cstr str) a b)
      (append (list (if (> (strlen a) 0)
                        (Str a) (Str str)))
              (if (> (strlen b) 0)
                  (regex_split_a regex (Str b))
                  null)))))

(bind-func regex_split_b
  (lambda (regex str)
    (let ((a:i8* (salloc 2048))
          (b:i8* (salloc 2048)))
      (sprintf a "")
      (sprintf b "")
      (rsplit (string_cstr regex) (string_cstr str) a b)
      (append (list (if (> (strlen a) 0)
                        (Str a) (Str str)))
              (if (> (strlen b) 0)
                  (regex_split_b regex (Str b))
                  null)))))

(bind-poly regex_split regex_split_a)
(bind-poly regex_split regex_split_b)

(bind-func regex_replace_a
  (lambda (regex str:String* replace:String*)
    (let ((res:i8* (salloc 4096)))
      (rreplace regex
                (string_cstr str)
                (string_cstr replace)
                res)
      (Str res))))

(bind-func regex_replace_b
  (lambda (regex str:String* replace)
    (let ((res:i8* (salloc 4096)))
      (rreplace regex
                (string_cstr str)
                replace
                res)
      (Str res))))

(bind-poly regex_replace regex_replace_a)
(bind-poly regex_replace regex_replace_b)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; 80211 streaming application
;;

;; load streaming lib
(sys:load "libs/core/stream.xtm")
(sys:load "libs/core/adt.xtm")

(bind-func input_80211
  (let ((push 8))
    (lambda (output)
      (lambda ()
        (qbuf_push output #x48)
        (qbuf_push output #x65)
        (qbuf_push output #x6C)
        (qbuf_push output #x6C)
        (qbuf_push output #x6F)
        (qbuf_push output #x21)
        (qbuf_push output #x21)
        (qbuf_push output #x21)))))

(bind-func scramble_80211
  (let ((pop 1) (push 8))
    (lambda (input output)
      (let ((shift:|7,i64|* (alloc)))
        (afill! shift 1 1 0 1 1 0 0)
        (lambda ()
          (let ((out 0) (j 0)
                (in:i64 (qbuf_pop input)))
            (dotimes (j 8)
              (set! out (^ (>> (& in (<< 1 j)) j)
                           (^ (aref shift 3) (aref shift 6))))
              (aset! shift 6 (aref shift 5))
              (aset! shift 5 (aref shift 4))
              (aset! shift 4 (aref shift 3))
              (aset! shift 3 (aref shift 2))
              (aset! shift 2 (aref shift 1))
              (aset! shift 1 (aref shift 0))
              (aset! shift 0 out)
              (qbuf_push output out)))
          void)))))

(bind-func encode_80211
  (let ((pop 1) (push 1))
    (lambda (input output)
      (let ((last_out:i64 0))
        (lambda ()
          (set! last_out (% (+ last_out
                               (qbuf_pop input))
                            2))
          (qbuf_push output last_out)
          void)))))

(bind-func chunk_80211
  (let ((pop 1) (push 1))
    (lambda (input output)
      (let ((bpsk_mod:|2,i64|* (alloc)))
        (afill! bpsk_mod 741343 -741343)
        (lambda ()
          (let ((in:i64 (qbuf_pop input)))
            (qbuf_push output (aref bpsk_mod in)))
          void)))))

(bind-func fir_half_80211
  (let ((pop 1) (peek 2) (push 8)
        (prepop 0) (prepeek 1) (prepush 8))
    (lambda (input:QBuffer:$<i64*>* output)
      (let ((rcc_taps:|16,i64|* (alloc))
            (prework (lambda ()
                       (let ((previous 0)
                             (current (qbuf_peek input 0)))
                         (doloop (j 8)
                           (qbuf_push output
                                      (/ (+ (* (aref rcc_taps j) (/ current 1024))
                                            (* (aref rcc_taps (+ j 8)) (/ previous 1024)))
                                         1024)))
                         void))))
        (afill! rcc_taps 0 -666 69361 157964 255889 351141 430952 484036 502648 484036 430952 351141 255889 157964 69361 -666)
        (lambda ()
          (let ((previous (qbuf_peek input 0))
                (current (qbuf_peek input 1)))
            (doloop (j 8)
              (qbuf_push output
                         (/ (+ (* (aref rcc_taps j) (/ current 1024))
                               (* (aref rcc_taps (+ j 8)) (/ previous 1024)))
                            1024)))
            (qbuf_pop input)
            void))))))

(bind-func output_80211
  (let ((pop 1) (print_to_log 0))
    (lambda (input)
      (let ((line 1))
        (lambda ()
          (let ((in:i64 (qbuf_pop input)))
            (if (= print_to_log 1)
                (begin
                  (printout "out[" line "]: " in)
                  (set! line (+ line 1))
                  (println)))
            void))))))


(bind-func main 100000000
  (let ((program (pipeline input_80211
                           scramble_80211
                           encode_80211
                           chunk_80211
                           (splitjoin DUP_RR fir_half_80211 2)
                           output_80211))
        (t 0))
    (lambda (iterations)
      (memzone (* 1024 1024)
        (begin
          (doloop (i iterations)
            (set! t (now))
            (program)
            (llvm_zone_reset (llvm_peek_zone_stack)))
          (println "runtime:" (/ (i64tod (- (now) t)) 44100.0) "seconds")
          (llvm_pop_zone_stack)
          void)))))

(main 9)

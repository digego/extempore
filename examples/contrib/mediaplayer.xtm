;;; gstreamer.xtm -- Simple GStreamer playbin example

;; Author: Andrew Sorensen
;; Keywords: extempore
;; Required dylibs: gstreamer

;;; Commentary:

;;

;;; Code:

;; load gstreamer lib
(sys:load "libs/external/gstreamer.xtm")

(bind-func _playbin_bus_call:GstBusFunc
  (lambda (bus msg data)
    (let ((playz (cast data GstElement*))
    (msgtype (tref msg 1))
    (time (tref msg 2))
          (seq (tref msg 4)))
      (cond ((= msgtype GST_MESSAGE_EOS)
       (printf "GSTREAMER: end of stream\n")
       (gst_element_set_state playz GST_STATE_NULL)
       (gst_object_unref (cast playz gpointer))
       1)
      ((= msgtype GST_MESSAGE_ERROR)
       (let ((debug:|1,i8*|* (alloc))
       (error:|1,GError*|* (alloc)))
         (gst_message_parse_error msg (cast error GError**) (cast debug i8**))
         (g_free (pref debug 0))
         (printf "GstError: %s\n" (tref (pref error 0) 2))
         (g_error_free (pref error 0))
         1))
      (else
             ;;(printf "MSG: %d:%s\n" msgtype (gst_message_type_get_name msgtype))
             1))
      1)))


;; path must be a valid URI to media
(bind-func xtm_play
  (lambda (path)
    (gst_init null null)
    (let ((playz (gst_element_factory_make "playbin" "play"))
    (bus (gst_pipeline_get_bus (cast playz GstPipeline*)))
    (gval:GValue* (alloc)))
      (g_value_init gval G_TYPE_STRING)
      (g_value_set_string gval path)
      (g_object_set_property (cast playz GObject*) "uri" gval)
      (gst_bus_add_watch bus (cast (llvm_get_function_ptr "_playbin_bus_call_native") GstBusFunc) (cast playz gpointer))
      (gst_object_unref bus)
      (gst_element_set_state playz GST_STATE_PLAYING)
      1)))

;; message loop
(bind-func _gst_main_loop_call
  (lambda ()
    (g_main_context_iteration null 0)))

;; call message loop
(define gst-main-loop
  (lambda ()
    (_gst_main_loop_call)
    (callback (+ (now) 2000) 'gst-main-loop)))

;; start GST msg loop
(gst-main-loop)

;; ;; start audio playback 1
(xtm_play "file:///Users/sorensen/Music/radiohead.aif")
;; ;; start video playback 1
(xtm_play "file:///Users/sorensen/Movies/stp_model.mov")
;; ;; start video playback 2
(xtm_play "file:///Users/sorensen/Movies/cave.mov")

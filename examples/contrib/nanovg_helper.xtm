;;; nanovg_helper.xtm -- Examples how to use /libs/contrib/nanovg_helper.xtm

;; Author: Yuya Chiba (cy.blue.9@gmail.com)
;; Keywords: extempore
;; Required dylibs: libnanovg

;;; Code:

(sys:load "libs/external/glfw3.xtm")

(bind-val width float 800.)
(bind-val height float 800.)

(bind-val window GLFWwindow*
  (begin
    (glfwSetErrorCallback (convert (get_native_fptr glfw_error_callback)))
    (glfw_init_and_create_interaction_window (convert width) (convert height))))

(sys:load "libs/external/nanovg.xtm")
(sys:load "libs/contrib/nanovg_helper.xtm")
(nvg_init)

(bind-val vg NVGcontext* (nvg_create_context))
(bind-val pixel_ratio float (glfw_get_pixel_ratio window))

(bind-func nvg_draw_loop
  (lambda (time:i64 delta_t:double)
    (nvg_clear)
    ;;(draw_a_line)
    ;;(draw_a_circle)
    ;;(draw_circle_loop)
    (glfwPollEvents)
    (glfwSwapBuffers window)
    (let ((next_time (+ time (convert (* 44100. delta_t)))))
      (callback next_time nvg_draw_loop next_time delta_t))))

(nvg_draw_loop (now) (/ 1. 30.))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                  Draw a line                ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; don't using nanovg_helper lib
(bind-func draw_a_line
  (lambda ()
    (nvgBeginFrame vg (convert width) (convert height) pixel_ratio)
    (nvgBeginPath vg)
    (nvgMoveTo vg 0. 0.)
    (nvgLineTo vg 400. 400.)
    (nvgStrokeWidth vg 5.)
    (nvgStrokeColor vg (NVGcolor 1. 0. 0. 1.))
    (nvgStroke vg)
    (nvgEndFrame vg)))

;; using nanovg_helper lib
(bind-func draw_a_line
  (lambda ()
    (nvgBeginFrame vg (convert width) (convert height) pixel_ratio)
    (nvgBeginPath vg)
    (xtm_draw_line vg 0 0 400 400 (xtm_rgba 255 0 0 255) 5)
    ;; (xtm_draw_line vg 2.5 2.5 402.5 402.5 (xtm_rgba 1. 0. 0. 1.) 5.5) ;; float type arguments are OK
    (nvgEndFrame vg)))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                 Draw a circle               ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; don't using nanovg_helper lib
(bind-func draw_a_circle
  (lambda ()
    (nvgBeginFrame vg (convert width) (convert height) pixel_ratio)
    (nvgBeginPath vg)
    (nvgCircle vg 400. 400. 100.)
    (nvgStrokeWidth vg 20.)
    (nvgStrokeColor vg (NVGcolor 0. 1. 0. 1.))
    (nvgStroke vg)
    (nvgFillColor vg (NVGcolor 0. 0. 1. 1.))
    (nvgFill vg)
    (nvgEndFrame vg)))

;; using nanovg_helper lib
(bind-func draw_a_circle
  (lambda ()
    (nvgBeginFrame vg (convert width) (convert height) pixel_ratio)
    (nvgBeginPath vg)
    (xtm_draw_circle_stroke vg 400 400 100 xtm_green 20)
    (xtm_fill vg xtm_blue)
    (nvgEndFrame vg)))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;                  Application                ;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(bind-func draw_circle_loop
  (lambda ()
    (nvgBeginFrame vg (convert width) (convert height) pixel_ratio)
    (let* ((x:i64 0)
           (y:i64 0)
           (dur:i64 40)
           (n:i64 (+ (ftoi64 (/ height (i64tof dur))) 1)))
      (dotimes (y 0 n)
        (dotimes (x 0 n)
          (xtm_draw_circle_stroke vg (* x dur) (* y dur) dur (xtm_leaprgba (xtm_leaprgba xtm_blue xtm_magenta (* (i64tof x) (/ 1. (i64tof n)))) (xtm_leaprgba xtm_yellow xtm_green (* (i64tof x) (/ 1. (i64tof n)))) (* (i64tof y) (/ 1. (i64tof n)))) 1)))
      (nvgEndFrame vg))))

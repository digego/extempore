;;; granulator.xtm -- an example of the granulator in audio_dsp

;; Author: Andrew Sorensen
;; Keywords: extempore
;; Required dylibs: libsndfile

;;; Commentary:

;; You'll need to load your own stereo AIFF asset

;;; Code:

(sys:load "libs/core/audio_dsp.xtm")
(sys:load "libs/core/audiobuffer.xtm")

;; load audio sample (put your stereo aiff file here!)
(bind-val abuf1 AudioBuffer* (AudioBuffer "assets/samples/xmas_carol.wav"))

(xtmX (println abuf1))

(bind-val gran1 [SAMPLE,i64,i64,SAMPLE]* (granulator_c 2))

;; lo and hi pairs make a range between which a random choice is between
(xtmX (gran1.iot 1000))     ;; inter offset time between grains
(xtmX (gran1.rlo 0.75))     ;; rate (low) 0.5 (half speed - octave down)
(xtmX (gran1.rhi 1.5))      ;; rate (hi)  2.0 (double speed - octave up)
(xtmX (gran1.dlo 1000.0))   ;; duration of grain (low) in samples
(xtmX (gran1.dhi 20000.0))  ;; duration of grain (hi) in samples
(xtmX (gran1.plo 0.0:f))    ;; pan low 0.0 stereo
(xtmX (gran1.phi 1.0:f))    ;; pan hi 1.0 stereo
(xtmX (gran1.alo 0.1:f))    ;; amplitude lo 0.0
(xtmX (gran1.ahi 0.4:f))    ;; amplitude hi 1.0
(xtmX (gran1.wet 0.9:f))    ;; wet/dry mix (0.0 input only - 1.0 grains only)

;; set playhead 6 minutes into the file
($ (AudioBuffer_set_playhead abuf1 0))

(bind-func dsp:DSP
  (let ((o1 (osc_mc_c 0.0))
        (o2 (osc_mc_c 0.0))
        (o3 (osc_mc_c 0.0))
        (o4 (osc_mc_c 0.0))
        (o5 (osc_mc_c 0.0))
        (o6 (osc_mc_c 0.0))
        (o7 (osc_mc_c 0.0))
        (o8 (osc_mc_c 0.0)))
    (lambda (in time chan dat)
      ;; modulate a bunch of the granulators params
      (gran1.iot (+ 5000 (ftoi64 (o1 chan 4800.0 0.1398))))
      (gran1.rlo (ftod (+ 1.0 (o2 chan 0.5 0.2))))
      (gran1.rhi (ftod (+ 1.0 (o3 chan 0.5 0.25))))
      (gran1.dlo (ftod (+ 4000.0 (o4 chan 3900.0 0.125))))
      (gran1.dhi (ftod (+ 4000.0 (o5 chan 3900.0 0.1))))
      (gran1.plo (+ 0.5 (o6 chan 0.5 0.123)))
      (gran1.phi (+ 0.5 (o7 chan 0.5 0.235)))
      ;; loop through audio file (change gran1.wet to adjust amount of input)
      (* 3.0 (gran1 chan time (AudioBuffer_read_looped abuf1 chan))))))

(dsp:set! dsp)

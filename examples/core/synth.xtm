;;; synth.xtm -- a simple little synth example

;; Author: Andrew Sorensen
;; Keywords: extempore

;;; Commentary:

;;; Code:

(sys:load "libs/core/instruments.xtm")

(make-instrument bass analogue)
(make-instrument lead analogue)

(bind-func dsp:DSP
  (lambda (in time chan dat)
    (cond ((< chan 2)
           (+ (* 1.0 (lead in time chan dat))
              (* 1.0 (bass in time chan dat))))
          (else 0.0))))

(dsp:set! dsp)


(bind-func analogue_oxygene_bass
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_PULSE)
    (set_osc1_tune inst 0.25)
    (set_osc1_pw inst 0.5) ;; square

    (set_osc2_amp inst 0.4)
    (set_osc2_wave inst PARAM_SAW)
    (set_osc2_tune inst 0.5)

    ;; filter
    (set_filter_frq inst 400.0)
    (set_filter_res inst 0.0)
    (set_filter_env inst 0.0 100.0 0.0 750.0)
    (set_filter_env_amt inst 300.0)

    ;; amplitude
    (set_amp_env inst 0.0 0.0 1.0 750.0)
    (set_gain inst 10.0)

    ;; polyphony
    (set_polyphonic inst) ;monophonic inst)
    (set_legato inst 1)
    void))


(bind-func analogue_oxygene_lead
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_SAW)
    (set_osc1_tune inst 0.5)

    (set_osc2_amp inst 0.4)
    (set_osc2_wave inst PARAM_SAW)
    (set_osc2_tune inst 0.50025)

    (set_osc3_amp inst 0.4)
    (set_osc3_wave inst PARAM_SAW)
    (set_osc3_tune inst 0.4996)

    ;; filter
    (set_filter_frq inst 1500.0)
    (set_filter_env_follow_frq inst 2.5)
    (set_filter_env inst 0.0 400.0 0.0 0.0)
    (set_filter_env_amt inst -1500.0)
    (set_filter_res inst 0.0)

    ;; set delay
    (set_delay_mix inst 0.3)
    (set_delay_time_left inst (* 0.125 .125 (/ 60.0 125.0) SRs))
    (set_delay_time_right inst (* 0.125 .125 (/ 60.0 125.0) SRs))
    (set_delay_fb inst 0.45)

    ;; amplitude
    (set_amp_env inst 150.0 0.0 1.0 600.0)
    (set_gain inst 15.0)

    ;; polyphonic
    (set_polyphonic inst)
    void))


(analogue_oxygene_bass bass)
(analogue_oxygene_lead lead)

(*metro* 'set-tempo 125.0)

(define R repeat)

(define test
  (lambda (beat dur)
    (playp 32 0 bass `(,@(R `((60 __ 60) (58 __ 60) (__ __ 55) (58 __ 55)) 4)
                       ,@(R `((62 __ 62) (60 __ 62) (__ __ 57) (60 __ 57)) 2)
                       ,@(R `((65 __ 65) (63 __ 60) (__ __ 65) (63 __ 60)) 2))
           (cosr 80 20 2) 1/4)
    (playp 32 0 lead 12 `(,@(R `(72 (__ __ 67) (63 __ 67) (__ __ 60) ,@(R __ 4)) 2)
                          ,@(R `(70 (__ __ 69) (67 __ 69) (__ __ 62) ,@(R __ 4)) 1)
                          ,(R `((69 __ 67) (65 __ 60) __ __) 2))
           (cosr 80 20 2) (* 2.0 dur))
    (callback (*metro* (+ beat (* .5 dur))) 'test (+ beat dur) dur)))

(test (*metro* 'get-beat 4) 1/4)

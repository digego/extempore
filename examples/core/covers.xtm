;;;
;;
;; A few fun covers to demo/test the "analogue" synth
;;

;; Author: Andrew Sorensen
;; Keywords: extempore

;;; Commentary:

;;; Code:

(sys:load "libs/external/instruments_ext.xtm")

;; load looper

(sys:load "./libs/core/looper.xtm")

;; make bass, lead, riff and pad
(make-instrument bass analogue)
(make-instrument lead analogue)
(make-instrument riff analogue)
(make-instrument pad analogue)

(bind-func dsp:DSP
  (lambda (in time chan dat)
    (cond ((< chan 2)
           (+ (* 1.0 (lead in time chan dat))
              (* 1.0 (pad in time chan dat))
              (* 1.0 (riff in time chan dat))
              (* 1.0 (bass in time chan dat))))
          (else 0.0))))

(dsp:set! dsp)

;;
;; analogue synth patches
;;

;; whiter shade of pale

(bind-func analogue_pale_organ
  (lambda (inst)
    (analogue_reset inst)
    
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_SINE)
    (set_osc1_tune inst 1.0)
    (set_osc1_pan inst 0.5)

    (set_osc4_amp inst 0.4)
    (set_osc4_wave inst PARAM_SINE)
    (set_osc4_tune inst 0.996)
    (set_osc4_pan inst 0.4)
    
    (set_osc2_amp inst 0.3)
    (set_osc2_wave inst PARAM_SINE)
    (set_osc2_tune inst 3.001)    
    (set_osc2_pan inst 0.5)    
    
    (set_osc3_amp inst 0.3)
    (set_osc3_wave inst PARAM_SINE)
    (set_osc3_tune inst 1.98)
    (set_osc3_pan inst 0.6)

    (set_noise_type inst PARAM_PINK)
    (set_noise_amp inst 0.02)

    ;; poor mans leslie speaker :)
    (set_flanger_rate inst 0.006315)
    (set_flanger_low inst 1.0)
    (set_flanger_high inst 3.0)
    (set_flanger_fb inst 0.2)
    (set_flanger_mix inst 0.3)
    
    ;; amplitude
    (set_amp_env inst 0.0 0.0 1.0 1000.0)        
    (set_gain inst 10.0)

    ;; polyphonic
    (set_polyphonic inst)    
    void))

;; drwho

(bind-func analogue_drwho_swoosh
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.0)

    (set_noise_type inst PARAM_WHITE)
    (set_noise_amp inst 1.0)
    
    ;; filter
    (set_filter_type inst PARAM_BPF)    
    (set_filter_frq inst 1500.0)
    (set_filter_res inst 0.9)
    (set_mod_matrix inst PARAM_LFO1 PARAM_FILTER_FRQ 1.0)    
    (set_lfo inst PARAM_LFO1 1000.0 0.0313812)

    (set_flanger_rate inst 0.08315)
    (set_flanger_fb inst -0.9)
    (set_flanger_mix inst 0.5)
    (set_flanger_low inst 1.0)
    (set_flanger_high inst 5.0)    

    ;; amplitude
    (set_amp_env inst 500.0 0.0 1.0 3000.0)        
    (set_gain inst 10.0)

    ;; polyphonic
    (set_polyphonic inst)
    void))


(bind-func analogue_drwho_bass
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_PULSE)
    (set_osc1_tune inst 0.5)
    (set_osc1_pan inst 0.4)
    (set_osc1_pw inst 0.55)

    (set_osc2_amp inst 0.4)
    (set_osc2_wave inst PARAM_PULSE)
    (set_osc2_tune inst 1.001)
    (set_osc2_pan inst 0.5)
    (set_osc2_pw inst 0.65)
    
    (set_osc3_amp inst 0.2)
    (set_osc3_wave inst PARAM_PULSE)
    (set_osc3_tune inst 2.0025)
    (set_osc3_pan inst 0.6)
    (set_osc3_pw inst 0.75)

    (set_osc4_amp inst 0.5)
    (set_osc4_wave inst PARAM_SINE)
    (set_osc4_tune inst 0.2501)
    (set_osc4_pan inst 0.5)
    (set_osc4_pw inst 0.5)
    
    ;; amp env
    (set_amp_env inst 5.0 1000.0 0.1 250.0)    

    ;; filter
    (set_filter_frq inst 700.0) ;; 700 hz
    (set_filter_env inst 5.0 200.0 0.1 250.0)
    (set_filter_env_amt inst 2500.0)
    (set_filter_res inst 0.25)

    ;; pitch env
    (set_pitch_env inst 100.0 0.0)
    (set_pitch_env_amt inst -0.025)

    ;; set reverb
    (set_reverb_mix inst 0.5)
    (set_reverb_size inst 0.8)

    ;; set gain
    (set_gain inst 5.0)

    ;; polyphonic
    (set_polyphonic inst)
    void))

(bind-func analogue_drwho_lead
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.6)
    (set_osc1_wave inst PARAM_SINE)
    (set_osc1_tune inst 1.0)
    (set_osc1_pan inst 0.25)

    (set_osc3_amp inst 0.5)
    (set_osc3_wave inst PARAM_SINE)
    (set_osc3_tune inst 1.00728)
    (set_osc3_pan inst 0.75)

    (set_osc2_amp inst 0.25)
    (set_osc2_wave inst PARAM_PULSE)
    (set_osc2_tune inst 2.003)
    (set_osc2_pw inst 0.5) ;; sqr
    
    ;; frq mod osc1
    (set_lfo inst PARAM_LFO1 1.0 3.128)    
    (set_mod_matrix inst PARAM_LFO1 PARAM_OSC1_FRQ 9.3738)
    
    ;; amp env
    (set_amp_env inst 50.0 100.0 0.8 3000.0)
    
    ;; filter
    (set_filter_frq inst 750.0) 
    (set_filter_env_amt inst 0.0) ;; no envelope
    (set_filter_res inst 0.95)
    (set_filter_env_follow_frq inst 2.0)
    
    ;; set reverb
    (set_reverb_mix inst 0.65)
    (set_reverb_size inst 0.4)
    
    ;; mono synth
    (set_polyphonic inst)
    (set_portamento inst 300.0)
    ;; (set_legato inst 1)
    
    ;; set gain
    (set_gain inst 6.0)
    void))


;; oxygene


(bind-func analogue_oxygene_riff
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_SAW)
    (set_osc1_tune inst 0.5)

    ;; filter
    (set_filter_frq inst 500.0)
    (set_filter_env inst 0.0 400.0 0.0 100.0)
    (set_filter_env_amt inst 300.0)
    (set_filter_res inst 0.6)

    ;; amplitude
    (set_amp_env inst 10.0 400.0 0.0 100.0)        
    (set_gain inst 20.0)

    ;; polyphonic
    (set_polyphonic inst)
    void))


(bind-func analogue_oxygene_bass
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_PULSE)
    (set_osc1_tune inst 0.25)
    (set_osc1_pw inst 0.5) ;; square

    (set_osc2_amp inst 0.4)
    (set_osc2_wave inst PARAM_SAW)
    (set_osc2_tune inst 0.5)
    
    ;; filter
    (set_filter_frq inst 400.0)
    (set_filter_res inst 0.0)
    (set_filter_env inst 0.0 100.0 0.0 750.0)
    (set_filter_env_amt inst 300.0)
    
    ;; amplitude
    (set_amp_env inst 0.0 0.0 1.0 750.0)        
    (set_gain inst 10.0)

    ;; polyphony
    (set_polyphonic inst) ;monophonic inst)
    (set_legato inst 1)   
    void))


(bind-func analogue_oxygene_lead
  (lambda (inst)
    (analogue_reset inst)
    ;; oscs
    (set_osc1_amp inst 0.4)
    (set_osc1_wave inst PARAM_SAW)
    (set_osc1_tune inst 0.5)

    (set_osc2_amp inst 0.4)
    (set_osc2_wave inst PARAM_SAW)
    (set_osc2_tune inst 0.50025)
    
    (set_osc3_amp inst 0.4)
    (set_osc3_wave inst PARAM_SAW)
    (set_osc3_tune inst 0.4996)
    
    ;; filter
    (set_filter_frq inst 1500.0)
    (set_filter_env_follow_frq inst 2.5)
    (set_filter_env inst 0.0 400.0 0.0 0.0)            
    (set_filter_env_amt inst -1500.0)
    (set_filter_res inst 0.0)

    ;; set delay
    (set_delay_mix inst 0.3)
    (set_delay_time_left inst (* 0.125 .125 (/ 60.0 125.0) SRs))
    (set_delay_time_right inst (* 0.125 .125 (/ 60.0 125.0) SRs))
    (set_delay_fb inst 0.45)

    ;; amplitude
    (set_amp_env inst 150.0 0.0 1.0 600.0)        
    (set_gain inst 15.0)

    ;; polyphonic
    (set_polyphonic inst)
    void))


;;;;;;;;;;; now the music


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; whiter shade of pale
;;


;; organ sound for lead and bassline
(analogue_pale_organ pad)

;; tempo 
(*metro* 'set-tempo 80.0)

; (define whiter-shade
;   (lambda (beat dur)
;     ;; melody
;     (rmap 32 0 (lambda (beat dur note length)
;                  (play pad note 80 (/ length 2)))
;           `(e4 _*8 d4 c4 b3 c4 d4 e4 c4
;             a4 _*5 b4 c5 (f4 e4) f4 _*4 d4 _
;             b4 _*5 c5 d5 (g4 f4) g4 _*3 f4 e4 f4
;             f4 e4 d4 c4 a3 b3 c4 d4 b3 c4 d4 e4 f4 e4 f4 d4)
;           `(9  _*8 1  1  1  1  1  1  1
;             6  _*5 1  1  1       5  _*4 2 _
;             6  _*5 1  1  1       4  _*3 1  1  1  1
;             1  1  1  1  1  1  1  1  1  1  1  1  1  1  1  1))

;     ;; bassline
;     (rmap 32 0 (lambda (beat dur note) (play pad (+ (eval note) -12) 80 2))
;         `(c4 b3 a3 g3 f4 e4 d4 c4 g4 f4 e4 d4 c4 f4 g4 b3))

;     (callback (*metro* (+ beat (* .5 dur))) 'whiter-shade (+ beat dur) dur)))


; (whiter-shade (*metro* 'get-beat 4) 1/4)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; dr who
;; 

(analogue_drwho_bass bass)
(analogue_drwho_lead lead)
(analogue_drwho_swoosh pad)


(*metro* 'set-tempo 125.0)

; (define drwho
;   (lambda (beat dur)
;     ;swoosh
;     (onbeat? 1 8 (play pad 60 60 10))
;     ;bass
;     (rmap 8 0 (lambda (beat dur note)
;                 (play bass note (cosr 100 20 2) 0.5))
;           `(,@(R 4 '(e3 _ e3)) ,@(R 3 '(b2 _ b2)) (b2 c3 d3)))
;     ;lead
;     (rmap 32 0 (lambda (beat dur note len) (play lead note (cosr 110 10 3) len))
;           `((b4 _*2 d5)
;             (b4 _*2 _)
;             (b4 g4 b3 _ d4 (d4 _ c4) b3 b3)
;             (b3 (a3 b3) b3 (b3 c4 d4) b3 _ b3 c5))
;           `((5 _*2 1)
;             (7 _*3)
;             (1/2  1/2  3/2 _  1/2  (1/2 _ 1/2) 1/2 1/2)
;             (1/2 (1/4 1/4) 1/4 (1/8 1/8 1/8) 1/4 _ 1/2 1/2)))
    
;     (callback (*metro* (+ beat (* .5 dur))) 'drwho (+ beat dur) dur)))

; (drwho (*metro* 'get-beat 4) 1/4)


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; oxygene
;;

(analogue_oxygene_bass bass)
(analogue_oxygene_riff riff)
(analogue_oxygene_lead lead)
;; reuse the dr's swoosh :)
(analogue_drwho_swoosh pad)

;; tempo
(*metro* 'set-tempo 125.0)

(define oxygene
  (lambda (beat dur)
    ;; (println (* 1.0 (modulo beat 32)))  
    
    ;swoosh  
    (onbeat? 1 8 (play pad 60 60 10))
    
    ;riff
    (rmap 32 0 (lambda (beat dur note amp) (play riff note amp 1/4))
          `(,@(R 16 '(48 48 48))
            ,@(R 8  '(50 50 50))
            ,@(R 8  '(48 48 48)))
          `(,@(R 32 '(90 70 70))))
    
    ;bass
    (rmap 32 0 (lambda (beat dur note) (play bass note (cosr 110 20 2) 1/4))
          `(,@(R 4 `((60 _ 60) (58 _ 60) (_ _ 55) (58 _ 55)))
            ,@(R 2 `((62 _ 62) (60 _ 62) (_ _ 57) (60 _ 57)))
            ,@(R 2 `((65 _ 65) (63 _ 60) (_ _ 65) (63 _ 60)))))
    
    ;lead
    (rmap 32 0 (lambda (beat dur note) (play lead note (cosr 80 20 2) dur))
          `(,@(R 2 `(72 (_ _ 67) (63 _ 67) (_ _ 60) _ _ _ _))
            ,@(R 1 `(70 (_ _ 69) (67 _ 69) (_ _ 62) _ _ _ _))
            ,(R 2 `((69 _ 67) (65 60 _) _ _))))
          
    (callback (*metro* (+ beat (* .5 dur))) 'oxygene (+ beat dur) dur)))

(oxygene (*metro* 'get-beat 4) 1/4)




;;; fft.xtm -- tests for xtlang code which calls external libs

;; Author: Ben Swift, Andrew Sorensen
;; Keywords: extempore

;;; Commentary:

;; tests for Extempore's FFT library

;;; Code:

(sys:load "libs/external/fft.xtm")

(xtmtest (bind-func test_fft
            (lambda (fft_length printq)
              (let ((signal:kiss_fft_scalar* (alloc fft_length))
                    (in:kiss_fft_cpx* (alloc fft_length))
                    (out:kiss_fft_cpx* (alloc fft_length))
                    (i 0)
                    (residual 0.))
                ;; fill signal buffer with noise
                (dotimes (i fft_length)
                  (pset! signal i (cos (/ (* (dtof TWOPI) (i64tof i)) (i64tof fft_length)))))
                (Complex_bufferize signal in fft_length)
                (if (= printq 1)
                    (dotimes (i fft_length)
                      (printf "in[%lld] = %f+%fi\n" i
                              (ftod (tref (pref-ptr in i) 0))
                              (ftod (tref (pref-ptr in i) 1)))))
                (fft in out (fft_config fft_length))
                (if (= printq 1)
                    (dotimes (i fft_length)
                      (printf "out[%lld] = %f+%fi\n" i
                              (ftod (tref (pref-ptr out i) 0))
                              (ftod (tref (pref-ptr out i) 1)))))
                (ifft out in (ifft_config fft_length))
                (dotimes (i fft_length)
                  (set! residual (+ residual (- (tref (pref-ptr in i) 0)
                                                (pref signal i)))))
                (set! residual (/ residual (i64tof fft_length)))
                (printf "Average residual = %f\n" (ftod residual))
                (if (< residual (* 0.00001 (convert fft_length float))) 0 1))))

         (test_fft (real->integer 4096) 0)
         0)

(xtmtest-result (test_fft 4 0) 0)
(xtmtest-result (test_fft 16 0) 0)
(xtmtest-result (test_fft 256 0) 0)

;; test real-only ffts

(xtmtest (bind-func test_fftr
            (lambda (fft_length printq)
              (let ((signal:kiss_fft_scalar* (zalloc fft_length))
                    (in:kiss_fft_scalar* (zalloc fft_length))
                    (out:kiss_fft_cpx* (zalloc fft_length))
                    (i 0)
                    (residual 0.))
                ;; fill signal buffer with noise
                (dotimes (i fft_length)
                  (pset! signal i (cos (/ (* (dtof TWOPI) (i64tof i)) (i64tof fft_length))))
                  (pset! in i (cos (/ (* (dtof TWOPI) (i64tof i)) (i64tof fft_length)))))
                (if (= printq 1)
                    (dotimes (i fft_length)
                      (printf "in[%lld] = %f+%fi\n" i (ftod (pref in i)) (ftod (pref in i)))))
                (fft in out (fftr_config fft_length))
                (if (= printq 1)
                    (dotimes (i (+ 1 (/ fft_length 2)))
                      (printf "out[%lld] = %f+%fi\n" i
                              (ftod (tref (pref-ptr out i) 0))
                              (ftod (tref (pref-ptr out i) 1)))))
                (ifft out in (ifftr_config fft_length))
                (dotimes (i (+ 1 (/ fft_length 2)))
                  (set! residual (+ residual (- (pref in i) (pref signal i)))))
                (set! residual (/ residual (i64tof fft_length)))
                (printf "Average residual = %f\n" residual)
                (if (< residual (dtof 0.0001)) 0 1))))

         (test_fftr 4096 0)
         0)

(xtmtest-result (test_fftr 4 0) 0)
(xtmtest-result (test_fftr 16 0) 0)
(xtmtest-result (test_fftr 256 0) 0)
